[head]Chapter 3: Druid Encounter[/head]
The second spring after those tragic events surrounding a dear friend and a Jackaloid occurred, you’re slowly beginning to walk nearer and nearer to Frontierwood forest. 

In spite of any…
*fake_choice
    # Fears that may linger ([color=${brawn_color}]Brawn++[/color])
        *set Brawn +1
        *set CH3_looming_thoughts "fears that may linger"
    # Disapproval from those in your village ([color=${agility_color}]Agility++[/color])
        *set Agility +1
        *set CH3_looming_thoughts "disapproval from those in your village"
    # Bitter memories you may have ([color=${cunning_color}]Cunning++[/color])
        *set Cunning +1
        *set CH3_looming_thoughts "bitter memories you may have"
*page_break

In spite of any ${CH3_looming_thoughts}, your adventurous side can only be held down for so long. Besides, this isn’t even the same forest. The other difference now is that you refuse to drag anyone along with you, not again.

Once upon a time, you hadn’t known better. You had that invincibility that youth granted you, the belief that nothing bad could touch you or the ones you love. Now, you’ll make better choices. You’re sure of it. You’ve seen what terrible things can happen when you're underprepared. From this moment onward, and for the rest of your life, [i]you will make better choices[/i].

As you walk alongside the rushing river, there happens to be a doe walking ahead of you, which you’ve been following...

*fake_choice
    # To observe its behavior! The way each creature goes about its own routine in the forest is absolutely fascinating! ([color=${cunning_color}]Cunning++[/color])
        *set Cunning +1
    # To see how long it takes to notice you! You’ll test your speed if it starts running… ([color=${agility_color}]Agility++[/color])
        *set Agility +1
    # Because you’re hungry… That doe could provide a couple days of good eating! ([color=${brawn_color}]Brawn++[/color])
        *set Brawn +1
*page_break

As the doe comes to a fork in the road, it turns away from either path, wandering deeper into the forest. Today, you’re feeling particularly bold, so you continue following the doe in spite of past experiences. If anything seems out of place, you’ll know when to run.

Once the doe has reached a spring, it begins to transform before your very eyes! The doe stands on its hind legs, its warm brown pelt almost melting away to reveal tanned skin. Narrow hooves uncurl and form lithe hands and feet. A wave of long, mahogany colored hair falls around her shoulders. Where there was a doe just a moment prior, now stands an elven woman clad in leaves, woven together with patches of bark-like leather and twine. These are characteristics of a druid!

If you had to guess, you’d say she was a wood elf. You haven’t seen many wood elves; a lot of them live deep in these forests. Druids are generally a peaceful kind, though they don’t typically appreciate outsiders in their territories. The opportunity to get a closer look presents itself.

You…

*fake_choice
    # Speak to the druid ([color=${cunning_color}]Cunning++[/color])
        *set Cunning +1

        You walk up to the druid, rather casually, barely containing your excitement. You ask her if she’s a real druid; the kind that really protect the forest and sleep under the shade of the trees.
        
        She seems unamused, but answers all the questions abruptly. Simple “Yes” and “No” responses to even your longer, more complicated questions. Perhaps a little inappropriately, you wonder aloud...

        “Are you alright, miss?”

        And that’s when it becomes clear. As much as druids don’t care for outsiders, there is something different about this one. Something seems to have truly gotten under her skin.

        
        “You may hear of our woes, little weedling, if only so that they may be heard…”

    # Watch the druid ([color=${agility_color}]Agility++[/color])
        *set Agility +1
        
        You hide in a relatively benign-looking bush, and stare at the druid. This is a rare opportunity; has anyone from your village ever even seen a druid? Well, now you can say you have.
        
        She sits down and lays her head back against a tree, letting out a lengthy sigh that borders on the territory of a groan. You think this is odd behavior for a free spirited druid to be exhibiting. They’re supposed to be happy, from what you’ve heard.

        You wonder what she could possibly be thinking about. Could something be wrong in the forest? Memories start to float up in your mind. A sunny day in the forest going horribly wrong. The possibility of something else terrible happening here causes you to shiver. Maybe this wasn’t such a good idea after all...

        She seems to stay that way, staring up at the branches for a couple of minutes, before lowering her head back down and locking eyes with you in your hiding spot.

        You freeze.

        *page_break

        “Your pursuit did not go unnoticed, little weedling.”
        
        You sheepishly waddle out of the bush. How silly could you have been? You aren’t a child anymore...
        
        “You want to know what ails me, don’t you?”
    
        You manage to meet the druid’s gaze; the emotions haven’t changed in the slightest. She just looks so dejected… You give a nervous nod.
        
        “Very well. You have an air of silence, and our feelings have reached an excess in our hearts. You may know what ails us, little weedling … if only to ease the pressure.”

    # Leave and explore deeper into the forest ([color=${brawn_color}]Brawn++[/color])
        *set Brawn +1

        But your instincts tell you otherwise. Better to not disturb these peaceful people of the forest with your villager ways. They avoid outsiders for a reason, you imagine.
        
        You wander in a perpendicular direction, deeper into the forest. The thick canopy of leaves and branches cast wide shadows, but it almost seems too dark for the time of day. If it gets too dark, you think, you’ll turn back. It’s still mid-day, so you don’t take this as a sign of danger.

        The earthy notes of the forest slowly become stronger, quite the plague on your nostrils. That doesn’t strike you as an oddity, at first. There are more trees here, more moss growing on the ground, and less interference from the nearby village. It just makes sense that it smells different here. You see movement in the pitch blackness up ahead; somehow, you manage to discount it as mere imagination.
        
        *page_break
        
        *image images/Mockgarden_Closeup.webp

        Suddenly, you see something whip toward you. It would’ve hit you, had you not felt a force pull you down from behind.

        Heart still pumping, you look up to see liquid seeping from a tree that was near you, also spotting a deep slash mark in the bark that looks burnt.

        Behind you is the druid from earlier, who presumably had pulled you back. Still holding onto your hand, she pulls you up.

        “Run!”

        *page_break

        As she takes off into the forest, you have enough sense left in your terrified brain to follow. 
        
        And so you both run. Fast. You’re rather out of breath by the time the two of you return to the spring where you’d initially split directions. The druid, more in charge of her stamina, isn’t panting nearly as hard as you are.

        Your lungs are burning, and your legs are sore. You push away the memory of feeling the exact same way not too long ago, running for your life in a similar forest. The druid keeps silent until you reclaim your faculties.

        “There’s a giant mockguarden in that part of the forest, weedling … I was hoping you’d avoid it.”

        *page_break

        As you continue to catch your breath, you take that in. A mockguarden? Of course it was a mockguarden! The strangeness of the forest suddenly makes sense and you quietly berate yourself for not realizing it earlier. Still, you’ve never seen a giant mockguarden. Just how large is it? 
        
        Perhaps your fellow village folk tell you all those stories about wandering too far for a reason. You take a moment to brood over your inability to take warnings before apologizing to the druid.

        “No, perhaps it was our own fault for not telling anyone. Little weedling, if you’ll indulge, you shall hear of what ails us, and spread this knowledge to your kin…”

        You affirm that you’ll hear what she has to say.

*page_break

Presumably, you’re meant to follow as she walks off. You’re already in this deep, so, you might as well follow, right?

As you’re led deeper into the forest, the druid introduces herself as “Milva of the Frontier Woods.” You think to correct her; it’s Frontierwood! On second thought, though, the druids probably just have a different name for it. At least it’s recognizable.

Milva takes you through a somewhat larger clearing in the depths of the forest, where you’re shocked to see plenty more druids, going about their day. Some tend to fruiting plants, others lay in the shade; it’s a rather calm and peaceful place. You feel both proud and a little intimidated to know you’ve been brought into a druid meadow. No one in your village has ever had the honor before.

The two of you end up at a more remote part of the clearing where you see a rather gruesome sight: a dying druid. At least, you hope it is dying; the slash across its chest is too deep for you to see any end to it, and much of the skin around the wound looks charred. You pity anyone who would have to live on with that wound...

*page_break

Of course, this druid was in the worst condition; it doesn’t take you long to notice that a small handful of similarly, if a little less severely, injured druids were laying in this area on makeshift cots with jugs of water and clusters of herbs by their heads.

The sight reminds you of something you’ve only seen once before: this is an infirmary. A makeshift, druid infirmary, but an infirmary nonetheless. Sickening memories of lingering rot and herbal scents mix with present ones; you cover your mouth and look down at your feet.

“These casualties,” Milva explains, “have become more commonplace in recent times, and are not limited to us druids. Plenty of the forest creatures had been dropping like flies as well.”

“Why?” you say under your breath. ”I don’t understand… is every forest cursed? Are they all dangerous places where the peaceful and innocent go to die?”

Milva answers your panicked pleas.

*comment Doing this before the check page because there is a bug in choicescript
*comment that allows you to reroll random chances.

*set GP_arg0 Brawn
*set GP_arg1 "simple"
*gosub_scene general AttributeSkillCheck
*temp CH3_Brawn_bool returnData

*set GP_arg0 Agility
*set GP_arg1 "simple"
*gosub_scene general AttributeSkillCheck
*temp CH3_Agility_bool returnData

*set GP_arg0 Cunning
*set GP_arg1 "simple"
*gosub_scene general AttributeSkillCheck
*temp CH3_Cunning_bool returnData
*set isMockguardenDiscovered true

*page_break
“I see you’re not unfamiliar to our plight… for that, little weedling, I apologize.”

The sight is such an overwhelming one that you have trouble arriving at the conclusion that Milva finally gave you: A gigantic Mockguarden has taken root here, where it hadn’t been before.

The naturally caustic species would spell a slow and painful death for any neighbors it found, and Frontierwood has become a new nesting ground for the sour shrooms.

You find yourself particularly moved by the druids’ plight. Perhaps you seek some type of glory out of this… or perhaps you want to help out of sympathy… or perhaps you simply seek to make right with the forest after what happened with Reese those short years ago...

You can’t be too sure, but one thing is certain! You will help these forest people as best as you can. You take a little time to think, wracking your brain for answers to this growing problem. Finally, you come up with some plausible ideas. 

You'll
*fake_choice
    # Convince some mercenaries at a nearby tavern to help (Simple [color=${brawn_color}]Brawn[/color] Check)
        [head]Brawn Check @{CH3_Brawn_bool Success|Failure}![/head]
        *set Brawn +1
        You make the somewhat inconsequential trek to a tavern near Frontierwood. The path leading through frontierwood is the only link between some smaller villages and the larger Davenport, so it’s a path naturally dotted with taverns and inns.
        
        As you enter the not-so-fine establishment, you find yourself assaulted by the overwhelming sounds of chatter, music, clinking of glasses, and other miscellaneous noises.
        
        Fighting through your senses, you manage to spot a group of particularly well armored adults with swords, shields, and all sorts of other dangerous-looking things.
        
        Now all you need to do is stand out.

        *if CH3_Brawn_bool
            
            You waste no time getting their attention. You climb up onto their central table and loudly announce your presence, to the disdain of the few who were talking.
            
            But who cares; there are more important things to be talking about and a whole forest to save.
            
            You project your voice, explaining in bold terms the plight faced by the forest druids. Sure, you have no money to pay them, but are they not people who do what they do for glory and for honor? Is that not what they stand for?
            
            You either gave a good speech, or they pitied you for how silly you looked; whatever the case, you seem to have gotten through to these mercenaries. A good handful of the strong ones, who brought with them a good amount of the weaker ones, agreed to help the inhabitants of Frontierwood.
            
            You feel hopeful. The mercenaries go to Frontierwood to fulfill their end, and send the runt of their group to drag you home.
            
            Stupid responsible adults...
            *page_break
            *goto CH3_Good_End
        *else
            You somewhat nervously approach one of the tougher-looking adults; he towers over you even though he’s sitting down. You gulp and give a light tug on his sleeve to get his attention. When he looks down at you, your blood turns to ice.

            “Need something, kid?”
            
            Woah… Stone cold. It takes a little while for your tongue to cooperate, but you manage to squeeze out the words and explain what’s going on in Frontierwood. The mercenary ponders your request and accepts, bringing along a few of his allies.
            
            It isn’t the large group of mercenaries you were hoping to gather together, but it will surely be enough, right? They are just going to be mowing down a bunch of shrooms, right? Something doesn’t sit well with you, despite this...
            
            As the small group of mercenaries head for Frontierwood, you go home and try to ignore your worries.
            
            *page_break
            *goto CH3_Bad_End
    #Steal some poison from the town apothecary and handle things personally (Simple [color=${agility_color}]Agility[/color] Check)
        [head]Simple Agility Check @{CH3_Agility_bool Success|Failure}![/head]
        *set Agility +1
        You know of one place and one place only that will have what it takes to beat back this great evil. You’ll need to acquire some fungal poison, and if you’re going to acquire enough of it, you’ll need to be less-than-upstanding in your acquisition.
        
        You make the rather lengthy trek back to your village and, navigating through the narrow areas between shops, slip into the apothecary’s shop through a back window. You have to time this just right; if someone’s in here, you’ll be caught immediately.
        
        *if CH3_Agility_bool
            Yes! Your patience is rewarded with pure silence; looks like you made it in without being sensed by the shopkeep!
            
            Trying not to give into the adrenaline, you bob and weave your way through the shop, searching for a particular group of vials. Reds, yellows, greens… ah, yes! Purples!
            
            Purple is the color of poison, after all. Or at least, the large skull and crossbones accompanied by a mushroom on the vials lead you to believe so.
            
            The fungal poison is labeled for its exact purpose, and exactly for your convenience. You stuff as many into your pockets as you can, and then...
            
            [i]Chime[/i]
            *page_break
            Uh oh. The shopkeep has returned! You quickly make your escape out the back window from whence you came. You linger just long enough to hear the shopkeep let out a dirty word!
            
            You don’t look back, but you feel a sense of mischievous satisfaction as you slip into an adjacent street, no one the wiser...
            
            Once you’ve returned to the part of Frontierwood just before the druid spring, you begin climbing a tree. 
            
            The only things still making any sounds in the dark part of the forest are the birds and squirrels; if you wanted to keep being able to make sounds, you’ll follow their example.
            
            The deeper you go, the denser the forestry gets, and you can certainly hop from tree to tree without much risk to yourself.
            *page_break

            The vials are small enough to fit into your slingshot. Since they’re made of glass, the force of the impact should be enough to shatter them, dosing the area in the potent potion. Just to be safe, you loosen the cork of the vials just before loading them individually into your slingshot.
            
            So, you take aim, hopping from tree to tree and moving deeper and deeper into the forest.
            
            For the first time in your life, you suddenly understand why adults enjoy the hunt. Little by little, you see the mycelium stalks start to shrivel and shrink into themselves as the glass vials shatter, coating everything in the poisonous purple potion.

            *page_break
            *goto CH3_Good_End
        *else
            Your excitement becomes your own undoing.
            
            The shopkeep, halfway through the door, whips their head around. You are caught in the act before it even began! You’d never even had a chance!
            
            You execute plan B, running past the shopkeep. You dash over the counter, knocking over some recipes. No matter. You grab for the blob of purple loosely defined in your immediate vision; that’s where the poisons are, you think.
            
            But you can’t think quickly enough; the shopkeep grabs you, their fingers digging into your arm. The shopkeeper immediately begins to yell at you, going off about the concoctions you knocked over and wondering what possessed you to enter through the back window. After a few minutes of venting, they slow down and begin questioning your odd behavior. They seem really mad, but...
            
            Once you’ve explained yourself, the shopkeep sighs. They travel into the forest often for supplies and understand the danger the giant mockguarden poses. They take pity on you and the druids’ plight, gifting you a couple vials of fungal poison for your troubles.
            
            “They were barely selling anyway…” the shopkeeper grumbled.

            *page_break
            
            You quickly get out of his sight.
            
            Once you’ve returned to the part of Frontierwood just before the druids’ spring, you struggle to climb a tree. You’re tired from all the walking you’ve done today, not to mention the running from earlier... 
            
            The only things still making any sounds in the dark part of the forest are the birds and squirrels; if you want to keep being able to make sounds, you’ll have to follow their example.
            
            The forestry, the deeper you go, is quite dense. That should hopefully be enough for you to be able to hop from tree to tree.
            *page_break

            As you travel through the trees, the pocket holding the vials catches on a sharp branch and rips. A few of the vials fall out before you're able to transfer them to your other pocket. Dang! 
            
            You now pull out the rest of the vials and your slingshot as you travel. It's not much, but it'll have to do...
            
            You’re sure you take down a couple. You’d remember better if you hadn’t fallen off one of the branches and hit your head on the way down.
            
            Milva has to rescue you from becoming shroom-food.
            *page_break
            *goto CH3_Bad_End

    # Consult the town priest (Simple [color=${cunning_color}]Cunning[/color] Check)
        [head]Simple Cunning Check @{CH3_Cunning_bool Success|Failure}![/head]
        *set Cunning +1

        *if CH2_followed_voice
            You know of only one true authority on banishing such evils... he did all he could for you two years ago, and you’re sure he’ll do what he can for you now, too.

            You make the rather lengthy trek back to your village and, navigating through the winding paths and random passerby, you approach the door of one of the more ornate homes in your village. And then you knock.

            You’re met with the priest from two years ago. Has he gotten shorter, or have you gotten taller?

            He frowns when he sees you; he’s always done this, ever since what happened. Maybe he blames you, or maybe he pities you. Who’s to say but the man himself? Not that you’d ever ask...

        *else
            You know of only one kind of authority that specializes in banishing evils: the town priest. He helped clear the forest of jackaloids after you ran into town.

            You make the rather lengthy trek back to your village and, navigating through the winding paths and random passerby, you approach the door of one of the more ornate homes in your village. And then you knock.
        *page_break
        
        Before he has the chance to speak, you begin to explain that there’s trouble brewing in Frontierwood.
        
        “Child… Slow down. Calm yourself. Can you tell me exactly what it is that you saw in Frontierwood?”

        *comment Base attributes should not be used in skill checks.
        *if CH3_Cunning_bool
            You explain exactly what you saw: a dark and quiet part of the forest, druids with blackened, wet wounds on their bodies, and the oversized mockguarden that Milva told you about. 
            
            “A giant mockguarden? In Frontierwood?”
            
            You try your best to explain just how large it is, easily larger than the old priest's entire house. The priest looks particularly troubled by this news.
            *page_break

            “They’ve grown that much? That’s certainly not good… This close to the village, they could pose serious harm.”
            
            You ask what he’s so worried about. He explains that, apart from the destruction they bring wherever they go, they seldom stop spreading unless they are knocked back. If they aren’t knocked back at Frontierwood, they’ll spread out of the forest and be troublesome for Sigel-knows-how-many villages… Maybe even this one!
            
            The priest senses your panic and speaks calming words.

            *page_break

            “I’ll handle it, child. You were right to come to me. You just go home and relax..."
            
            You nod hesitantly, thank the priest, and go home. It looks like things will be okay...
            *page_break
            *goto CH3_Good_End
        *else
            You try to recall what you can: a dark and quiet part of the forest by the spring, druids with slash wounds, and Milva spoke of some kind of creature whose name starts with an M...
            
            “A Mogfish? In Frontierwood?”
            
            Yes! That was definitely it!
            *page_break

            The priest gives you a confused and concerned look and explains that he just went to take a look at the mogfish territory a few days ago and didn't see anything out of the ordinary then.
            
            However, knowing the dangers that mogfish pose, the priest agrees to bring some supplies and take another look. The Mogfish are only present in a particular part of the forest; he’ll lay some strong herbs and say a few prayers to stop them from leaving their territory, but that’s really all he’ll be able to do.
            
            You thank him, nonetheless. You’re glad to have his aid.
            *page_break
            *goto CH3_Bad_End

*comment Good Ending -----------------------------------------------------------
*label CH3_Good_End
A few days pass. You’re confident that the mockguarden has been knocked back from Frontierwood, but you just can’t help but wonder… So you make the walk back to that little spring, and the clearing, and...

Everyone’s still there. That’s definitely a relief. 

Everyone seems a little put off by the presence of an outsider unaccompanied, and they openly stare, some even making a face at you, but you push past it. You ask around until you’re able to find Milva. You tell her what you did, and you ask her if the mockguarden has cleared up.

Milva gives you a smile; the first one you’ve seen her give. It reminds you of dapples of sunlight peeking through a forest canopy.

“Thank you, young sapling. We have our traditions, but perhaps there are times when we ought to seek the help of outsiders…”

She confirms what you were hoping for; your plan worked. You’re glad. You’re so very glad. It feels like a weight has been lifted off your shoulders.

“Thank you, young sapling. We have our traditions, but perhaps there are times when we ought to seek the help of outsiders…”

You’ve grown. You can handle the dangers of the forest now in ways that you wouldn’t have ever thought to try all those seasons ago...

You take this realization not to fuel any hubris, but to fuel a growing child’s much-needed sense of self-confidence.

You’ve proven yourself an ally to the druid enclave of the Frontier, and are welcome here any time for the foreseeable future.

*set CH3_ending "good"

*finish

*comment Bad Ending ------------------------------------------------------------
*label CH3_Bad_End
A few days pass. You’re really worried about the druids in Frontierwood, and you’re unable to stop thinking about it, until finally… You make the long and anxious walk back to that little spring, and the clearing, and...

It doesn’t feel nearly as dense or lively as it was. You feel a strong sense of unease. 

No one gives you any attention. You keep trying to find Milva, and it takes you a while. She seems to be brooding while loading some supplies into a makeshift container.  Desperately, you walk up to her. You tell her what you did. You ask her if the mockguarden has cleared up. You really want her to say yes.

Milva gives you a frown, a strong frown.

“Young sapling… We applaud your efforts, really, we do, but there really wasn’t much  you could do… You bought us a little more time, and for that we’re thankful, but this side of the forest no longer welcomes us. We will hopefully be able to make a home on the other side...”

Your heart sinks. It’s true, then; forests are cursed! There’s no fighting the dark forces that seek to terrorize these woodlands from the inside...

Milva gives you a look of pity. She doesn’t seem particularly cross with you. Maybe she’s as accepting of the natural condition as you’re feeling right about now?

*page_break

“This isn’t your first time, is it?” 

You give her a curious look. 

“It won’t be your last, either. I can see it in you… An adventurous spirit.” 

Then, unexpectedly, Milva embraces you. You’re not wholly sure what to make of this...

“Hear this, young sapling… Your actions bought us a few extra days to gather ourselves. At expense only to yourself, you sought to help us, and you made as significant of a difference as you could. You’ve done more in just one day than some people do in their entire lives. It might not feel like much to you, but to us, it means everything.” 

You try to get out the words. You try to tell her that it just doesn’t count if you fail. But, with your face pressed up against her abdomen, you’re bound to silence. 

“From this day on, absolve yourself of your past. Today, you start anew with the forest, as it does with you. The tree of life forgives those who seek to water its leaves.” 

You don’t understand her druid expression, but on that day, what she said to you finally resonated. You did what you could; you’re getting too old to keep reliving every possibility… You have to grow up.

In your heart of hearts, you apologize to Reese for the last time.

*set CH3_ending "bad"

*finish
