*label chapter4
*comment temporary variables
*temp option
*temp bought
*comment Temp variables for Stat Designation Page
*temp Attr_PNT_Total 0
*temp Stored_Attr_Total 0
*temp Increase_Attr false

*set coin 5

*if debug_reallocation = true
    *goto CH4_Reallocate_Menu
[head]Chapter 4: The Mix-up[/head]
The charming hustle and bustle of the town market reaches your ears long before you enter the busy street. There are multiple shops around you, each bursting with different items for sale and customers hovering in and out of open doorways. It’s been a few years since the incident with the druids and the giant mockguarden. Now, your job has just finished a huge order and the coin just came in. With your pockets now heavy with silver, you decided that you were going to spoil yourself today for all of your hard work. There are some items you have had your eye on and now is as good a time as any to purchase something new.

Along your way through the market, you see two people arguing. It’s impossible to hear what they’re arguing about, as it’s drowned out by the many people around you going about their day. However, the furrowed brow and growing sneer from the one facing you is enough to tell how heated this argument truly is. You walk on the other side of the path, trying to avoid getting caught in the crossfire. One looks over at you and you catch their eye. You can’t get a good look at them, but one detail you can see is the scar they have across their left eye. Before anything else can happen, a guard gets in between the two of them and breaks up the fight.

*page_break

*image images/Summerlight_finished.webp

Deciding that it is none of your business, you continue your way into the market. It is a lot busier than normal at this time of day, but not unexpectedly so. The dwarves are in town. Their special brand of gruff conversation and large stock of unique wares draws many out from their houses and into the streets, eager to see what new wonders the dwarves might bring. 

Once you get there, you walk over to the,

*fake_choice
    # Bakery. It is run by a dwarven baker and they rarely visit. They are said to have the best bread in the land.
        Their cart is surrounded by people, many of them munching on their treats, still steaming from the oven.. Behind the cart, there are two dwarves. One has their back to the crowd, pulling out fresh loaves from the oven and swiftly filling the empty space with more items ready to bake. The second dwarf has a tired look in their eye, but is still dutifully taking orders and tossing a joke or two at customers who came back for seconds. Occasionally the one taking orders offers some idle conversation. 
        
        While you are waiting for your bread to come out of the oven, the dwarf stands next to you.
        
        "You look like a traveler. I'd leave as quickly as possible if I was you." He said while leaning against the table. “On our way here we saw some evidence of Ichor-swogs along the road.”

        “How far out?” You ask, your eyes drifting to the forest almost instinctively.

        “Two or three days, maybe. Though, it seems like your lord has already started precautions. There are a lot of armed men here.” The dwarf looks around. You join him and note the now obvious amount of guards in the area. At first glance, you might have thought there were more because of the larger market crowd that day. However, guards didn’t need to be armed to the teeth to watch over civilians. They were preparing for something.

        “We aren’t going to be here for much longer. Packing up tomorrow. Enjoy your bread.”

        
        *set faction "guards"
        *set item "bread"
        *set bought "bread"
        *set coin -2
        *set CH4_market_item "bread"
        
    # Library. You have been looking at a book for weeks and you finally have the money to get it.
        The library is never really busy, since not many people know how to read beyond their family name. However, the Flames of Sigel requires there to be at least a small store for them to read and study while they stay. They visit at least once a month and that's enough to keep the library running. Although, there are a lot more people here than usual. You wonder if any of their priests have come to town.  
        
        You walk over to a book that you have been thinking about getting for a while now, and the librarian notices you.

        “I’d grab that book fast. There are many priests around and a couple of them have commented on that book,” she says while walking over. There's a sweet smile on her face, but her eyes have narrowed in on you like a hawk. You get the feeling she heard the coin in your pocket and is eager to rent out some books for a fee. 

        “Anything good?” You flip over the book and admire the cover.

        “Nothing but praise, although… There does seem to be a lot of them around. I hope nothing has happened.” As she mentions this, you take a closer look at everyone and notice that there are a lot of priests. Not all priests dress alike, but you note the symbol of the Flames of Sigel on their clothes or worn as jewelry. It’s odd; there are double the amount you normally see when they visit.

        “The last time I saw this many was about five years ago,” the librarian says as she puts another book away on a nearby shelf.

        “I hope that it’s nothing too serious,” you comment, looking at all the priests.
        
        *set faction "priests"
        *set item "book"
        *set bought "book"
        *set coin -3
        *set CH4_market_item "book"
        
    #Blacksmith. Your dagger is old and falling apart. You have been meaning to replace it.
        The blacksmith isn't particularly busy today. Only a handful of people are actually looking at his wares; everyone else seems to just be standing off to the side and chatting about this and that. Although you notice there are many groups of unfamiliar looking people around, all of them visibly armed and some armored. The sight of all these strangers makes you a little nervous. 
        
        You see the blacksmith teaching their apprentice when you walk up and start looking at the freshly polished daggers.

        “Looking for a new weapon?” the blacksmith asks, leaving their apprentice to work.
        
        You nod, turning your attention back to the rack of daggers. 
        
        “That’s probably a smart idea. Which one are you looking at?” the blacksmith walks over and brings down a few daggers.

        You point out the iron one with a fancier leather handle, and the blacksmith hands it to you. The leather feels comfortable in your hand, if a little stiff. It’ll conform to your grip soon enough. 

        “Why’s that?” you ask him, tossing the blade between your hands a few times and getting a good feel for the weight. It feels well balanced, definitely worth the price.

        “Because there are a lot of mercenaries out and about,” he replies, pointing to some of the armed groups of people you had spotted in the crowd.

        “Huh,” you say while looking around.

        “I haven’t seen this many in about five years.”

        You nod and hand over the coin.

        “I’d be careful walking home tonight, it looks like we have multiple groups here and not all of them get along,” he explains before he walks back towards his apprentice.
        
        “Brig! I told you, not while it’s so hot! You’ll warp the metal!”
        
        *set faction "mercenaries"
        *set item "dagger"
        *set bought "dagger"
        *set coin -5
        *set CH4_market_item "dagger"
*page_break

You find it odd that there are so many ${faction} around. The last time you saw this many was… Five years ago, when you went out exploring with Reese. The memory of that time is enough to make you nervous. Maybe you should visit them again soon. However, first you need to take your ${item} back to your house and put it away. Maybe you’ll visit after-
*page_break

[b] SCREAM [/b]

*page_break

A loud, anguished scream comes out of the house you are passing. The sudden sound causes your whole body to jump, every sense in your body now on high alert. Confused, you look over at the house, deciding what to do.

*choice
    #You run in.
        Your eyes quickly scan over the inside of the first room, but there isn't much to note. A rug and household items littered about on the floor. As you rush into the main room, you discover the source of that terrible scream. There is a body lying in the center of the room, each of its four limbs sprawled in different directions, with each looking more unnatural than the last.

        There is blood beneath them, a steadily growing pool that is soaking the carpet and no doubt staining the wood floor beneath it. For a moment, you feel your stomach churn, your last meal threatening to reappear on the floor at any second.

        You swallow, take a deep breath, and focus on the mystery in front of you. When you walk up to the body, your steps both cautious and careful, you notice that it is [codex:codex_hl_Lord_Odel]Lord Odel[/codex]. Lord Odel is a lord from a neighboring town. Not many like him, but you didn't think it was this bad.
        
        *set isOdelDiscovered true
        *set CH4_murderer "run"
        *set option "run"
        *goto C4_Chase
    
    #You ignore it, many things happen in this town, you don’t need to be a part of it.
        
        *image images/knife_no_background.webp
        
        The scream, though alarming, has nothing to do with you. More often than not, getting involved in random situations around town often leads to bad luck. It isn’t worth the risk.

        As you continue walking towards your house, someone bumps into you. You turn, expecting an apology or at least an acknowledgement, but the person lengthens their stride and continues on their way. They have a hood on and you can’t see their face. You check your pockets, only to find your ${item} missing, replaced with a [codex:codex_hl_Knife]bloody iron knife[/codex].
        *set isKnifeDiscovered true

        When you realize what you are holding, a guard walks up to you and aggressively grabs your arm.

        “You are under arrest for the murder of [codex:codex_hl_Lord_Odel]Lord Odel[/codex]!”
        *set isOdelDiscovered true
        *set item "bloody dagger"
        *set CH4_murderer "ignored"
        *set option "leave"
        *page_break
        *goto guard
        
*label C4_Chase
When you look up, you see a shadowed figure running through the back door. 

*fake_choice
    #Cut him off
       Your body reacts before your mind can catch up. You go back through the front door and start sprinting in the direction you saw them go. Even though you took off with the speed of a cat, you don’t see the shadow anywhere. As you run, your eyes dart to and fro, slowing for a moment at each alley, trying to catch a glimpse of the murderer. However, there is no sign of them. The mysterious figure seems to have vanished into thin air.
       
       You slow your frantic sprint, eventually coming to a stop at the mouth of another alley. Your throat begins to burn a little as you try to catch your breath. You're no stranger to running, but taking off in a dead sprint for so long isn’t really your strong suit. Just as you are finally starting to breathe normally, a guard suddenly walks up to you and harshly grabs your arm.
       
       “You are under arrest for the murder of Lord Odel and fleeing the scene of the crime!”
       
       *set CH4_murderer "cutOff"
        
    #Chase after him
        You run through the building, not noticing as one of your boots splash into the pool of blood.

        Outside, you see the cloaked figure running down an alley. You run into the alley right after it, remembering that they connect. You see the cloaked figure running through the alley.

        You grab his arm and just manage to dodge out of the way as a knife is suddenly slashing through the air where your head had been mere moments ago. You are forced to let go of his arm to get away from the attack completely. The hood on the mysterious figure moves slightly and underneath it you can see a familiar scar across his left eye.

        Dodging an attack that you weren’t prepared for set you off balance and put you too far away from the murderer. The figure takes advantage of the distance and swiftly dashes to the wall, beginning to climb. You go to follow him when-

        “Stop!” a guard shouts at you. For a moment, you're glad. The guards can back you up, help you chase down the murderer. You turn, ready to thank them for their assistance. 

        “You are arrested for the murder of Lord Odel!” The guard runs towards you and grabs your arm.
        
        *set CH4_murderer "chase"

*goto guard

*label guard
“You have the wrong person!” You try to explain in a panicked rush.

“Then why were you the only one spotted leaving the scene of the crime?” the guard asks, dragging you towards the carriage.

*if (option = "run")
    “I was walking home and I heard a scream in the house. I didn’t know something happened until I walked in there!” You struggle against the guard.

    “If you heard it, why didn’t you alert us?” The guard pulls harder at your arm, his grip growing so tight it begins to hurt.

    “Because there wasn’t time! I could have gotten in there in time and saved his life!”


*if (option = "leave")
    “I was walking home and I heard a scream in the house. I didn’t know something happened!” You struggle against the guard.

    “If you heard it, why didn’t you alert us? And why do you have the murder weapon?” The guard pulls harder at your arm, his grip growing so tight it begins to hurt.

    “Because I didn’t see any of you yet and the murderer planted this on me! They went that way!” You try to point in the direction they went in when the guard just pulls on you rougher.
    
The guard grabs you by your bicep and wrist, twisting them upwards, and starts walking you towards the carriage. Your panic begins to transform into adrenaline, clearing your mind. You know you’re innocent, and you also know that no matter how much try and tell the guard you had nothing to do with this crime, the evidence against you is too good. It only takes you a split second to make your decision. You bend the elbow in the guard's grasp, spinning towards him and using the momentum to shove his shoulder with your free hand. Not expecting it, the guard falls face first to the ground.

Mere moments after he hits the ground, you take off in a sprint towards a nearby alley and climb over the wall. You drop to the ground, startling a few people who were close by. You keep your eyes down, fixing your clothes as you walk into the crowd. It doesn’t take much to blend in with so many people around. Even so, you remain vigilant as you start heading home. 

You slip inside the familiar building, the sight of your home almost taking you by surprise for a moment. It’s exactly the way you left it. So normal and mundane. You were once the same, and somehow in a matter of a few hours, you returned to this place as someone completely different. There is no time to overthink things. After grabbing your clothes, some food, and a few items of importance, you start to plan your escape. You were lucky to get away, but the guard would rally his allies and search the whole town for you soon.

It is night and you decide to:

*choice
    
    #Go to the cemetery to say one last goodbye to your childhood friend.
        *goto C4_Cemetary

    #Take the side streets and leave.
        *goto C4_Leave
    
    #Take the path you saw to the forest a few weeks ago.
        *goto C4_Caught
    

*label C4_Cemetary
*set CH4_visited_reese_grave true
You decide to say one last goodbye to your childhood friend, Reese. You would visit their grave from time to time and tell them what has happened since they’ve been gone. You haven’t visited in a long time and this seems like your last chance.

You make your way through town, using the alleyways to stay out of sight from the patrolling guards, until you make it to the cemetery. Once there, you take a familiar path towards your friend, not noticing the person following you.

[b]What do you tell your late friend?[/b]
*fake_choice
    #I miss you.
        “Hey, Reese, long time no see. Before I go, I just wanted to say that I really miss you. You were the last person in the world to deserve the fate you were given. Seriously! I still have yet to meet another person as kind hearted as you were, and I want you to know that. Even when I’m gone, I’ll always be thinking of you.”

        You pause and take a moment to stare at Reese’s name carved upon the headstone. Closing your eyes, you can still see their smile. You can hear their laugh in your ear, feel their arm slung over your shoulder. You were close enough to be mistaken for siblings, and sometimes, you both pretended you were. More than anything, you wish Reese was still by your side.

        Using your sleeve, you clean the dirt and grime from the headstone. Your fingers trace over Reese’s name one last time. “I really miss you, old friend.”

    #I'm leaving now.
        “Hey, Reese. Some things have come up and, well, I have to get out of here. I just want you to know that even if I never come back to this place, I’ll still be thinking of you. In a way, I guess it’s better to get out of this town and explore what the world has to offer. Maybe bringing your memories with me will help you to see the world as well!"

        You pause and take a moment to stare at Reese’s name carved upon the headstone. Closing your eyes, you can still see their smile. You can hear their laugh in your ear, feel their arm slung over your shoulder. You were close enough to be mistaken for siblings, and sometimes, you both pretended you were. More than anything, you wish Reese was still by your side.

        Using your sleeve, you clean the dirt and grime from the headstone. Your fingers trace over Reese’s name one last time. “I hope you can watch over me, old friend.”

    #Someone framed me.
        “Someone murdered Lord Odel,” you say bluntly to the stone. The words tumble out of your mouth quickly after that. “I don’t know who, but they got away. The guards seem to think I did it. That jerk who did it managed to leave some evidence on me, so I’m going to have to leave. This will probably be the last time I’m able to visit.”

        You pause and take a moment to stare at Reese’s name carved upon the headstone. Closing your eyes, you can still see their smile. You can hear their laugh in your ear, feel their arm slung over your shoulder. You were close enough to be mistaken for siblings, and sometimes, you both pretended you were. More than anything, you wish Reese was still by your side.

        Using your sleeve, you clean the dirt and grime from the headstone. Your fingers trace over Reese’s name one last time. “May we meet again, old friend.”

[b]As you stand, you hear the crunching of leaves behind you.[/b]
*fake_choice
    #Jump behind Reese’s grave.
        *image images/Cel_Storm.webp
        As the coward you are, you jump behind your friend’s grave, and use it as protection against the unknown intruder. Upon closer examination, however, you notice that it’s a priestess!

        “I’m sorry, I didn’t mean to startle you. I just happened to see you out here. Why are you here so late? Don’t you know how dangerous it is to be out at night?” she asks you while walking closer.

    #Turn to see who it is.
        *image images/Cel_Storm.webp
        You whip around to see a priestess.

        “I’m sorry, I just happened to see you out here. Why are you here so late? Don’t you know how dangerous it is to be out at night?” she asks you while walking closer.

    #Defend yourself!
        *image images/Cel_Storm.webp
        *if (item = "bread")
            You turn around, whipping out the best weapon you have: the cure to starvation. As you point your bread at this stranger, you manage to catch a glimpse of her: a priestess.

            “I’m fine, thank you. I just had dinner a few hours ago. Anyways, why are you here so late? Don’t you know how dangerous it is to be out at night?” she asks you while walking closer.

        *elseif (item = "book")
            You turn around, whipping out the best weapon you have: knowledge. As you point your book at this stranger, you manage to catch a glimpse of her: a priestess.

            “Oh wow, that’s a good one. It’s very popular amongst The Flames of Sigel, and rightfully so. Anyways, why are you here so late? Don’t you know how dangerous it is to be out at night?” she asks you while walking closer.

        *elseif (item = "dagger")
            You turn around, whipping out the best weapon you have: sharp steel. As you point your knife at this stranger, you manage to catch a glimpse of her: a priestess.

            “If you go around pointing dangerous toys like that at strangers, you’re not going to make any friends. Anyways, why are you here so late?” she asks you while walking closer. “That knife won’t be able to protect you from everything the dark night has to throw at you,”

        *elseif (item = "bloody dagger")
            You turn around, whipping out the best weapon you have: a hungry blade. As you point the weapon that just murdered Lord Odel at this stranger, you manage to catch a glimpse of her: a priestess.

            “By the gods! If you wave that weapon around, people really are going to think you murdered him! Anyways, why are you here so late?” she asks you while walking closer. “That blade won’t be able to protect you from everything the dark night has to throw at you.”
        
        *else   
            *bug_continue No item is being held.

“I was just saying goodbye to my friend. I don’t think I’ll be able to visit them again,” you say as you quickly grab your stuff.

“Why not? Are you going somewhere?”  The priestess walks around a grave, steadily getting closer to you. 

“I’ve got mixed up in something I had nothing to do with and I have to leave.” You start heading towards the gate. 

“It’s too dangerous to leave this late, why can’t you stay until the morning?”

“Because I don’t know when they are going to come get me.”

“Nothing can be bad enough to go into certain death. If you are so worried, why don’t you come to the local temple and stay there?”  

*page_break

*image images/mercenary_transparent_background.webp

Before you have the chance to decide, a new voice cuts through the darkness.

“I’m not sure the temple would accept ${pronoun_them}. Lord Odel was murdered and I think this is the person they are after.” A man strides out of the shadows, wearing a cloak and a sword on his back. He doesn’t look like any guard you know, so he must be a mercenary.

[b]What do you say?[/b]
*fake_choice
    #I'm innocent!
        “You have the wrong guy, I had nothing to do with that,” you say as you start backing away from the mercenary and the priestess.

        “Even so, you are the one everyone is after.” The mercenary crosses his arms and stares at you. His eyes are unnaturally still, like the gaze of a predator mere moments before lunging at its prey. 

    #Leave me alone.
        "I was just trying to say goodbye to my friend before I move on to the next town in hopes of some peace and quiet. Now please, leave me be."

        “Unfortunately, peace and quiet is no longer an option for you. You are the one everyone is after.” The mercenary crosses his arms and stares at you. His eyes are unnaturally still, like the gaze of a predator mere moments before lunging at its prey. 

“I don’t think a murderer would visit their friend’s grave the day of the murder. They would visit the day before so they can leave as quickly as possible,” the priestess comments, looking towards the mercenary. Her pretty features are pulled into a frown as she seems to note the weapons on the man.

“You're right,” the man says, “A smart one would. To even plan a murder like this you would have to be very smart about it. But you? You were seen outside of the house running in. No murder would be that obvious. However, the guards don’t seem to care. They want the person they found caught and tried. ” The mercenary walks around the graves, his gait casual but his eyes never wavering.

“They don’t see that the murder was done by a skilled assassin, not some random person in the wrong place at the wrong time. They’ll hang you on the spot.” He stops a little bit away from you.

“All the more reason for me to leave. Now, if you’ll excuse me,“ you say as you creep towards the gate.

*page_break

“You won’t make it far without help.” The priestess steps forward. “There are dangerous creatures outside. I could take you to the temple and protect you from the town’s ire.” 

And do what? Pray that the problem will solve itself?” the mercenary scoffs, “Come join me. We’ll find out who did this and prevent them from killing again.”

The mercenary and priestess glare at each other before suddenly turning their attention to you.

*choice
    #"I choose to go with the priestess and stay at the temple."
        "Welcome to the Flames of Sigel. I'm sure your friend would be proud of you. Come along. We'll head to the temple when we are done here." The priestess leads you to the local church.
        
        *set faction "divine"
        *finish

    #"I choose to go with the mercenary and find out who did this."
        "We'll find who did this, now we need to get you out of here. Thankfully I know some people nearby and you can stay there for the time being." The mercenary takes you into a building.
        
        *set faction "merc"
        *finish

    #Think about decision. (Reallocate Attribute Points)
        *set Stored_Attr_Total (Attr_PNT_Total + Brawn) + (Agility + Cunning)
        *page_break
        *goto CH4_Reallocate_Menu

*label C4_Leave
You need to get out of town as soon as possible. The watch is on high alert, and if you use the cover of night you could get out of here.

You leave your house through the back window and begin using all of the back alleys to get to the edge of town. When you turn the corner of the fourth alley, you see a guard walking in your direction. Panicked, you turn around to find a place to hide. Luckily, you spot a barrel large enough to hide you from view.

“I wouldn’t hide there if I were you. When the guard flashes their lantern this way, your arm will cast a shadow along that wall,” a voice quietly calls to you in the darkness.

*page_break

*image images/mercenary_transparent_background.webp

“The crate will hide you better. I’d move fast if I were you.” You quickly follow the voice’s advice and move to the crate, just as the guard walks by. The lamp light moves over the barrel, illuminating  the spot you were just hiding in.

After the guard passes, you hear a thump behind you. You turn and see a man in a cloak with a sword on his back. He doesn’t look like a guard, so he must be a mercenary.

“Now, why are you out here so late at night? And what is with the stuff?” he asks, nudging your bag with the toe of his boot.

*page_break

You grab the bag off the ground and hold it to your chest. “What’s it to you?” 

“Whoa now! Just asking a simple question. Not many are out this late, and honestly, carrying all that stuff makes it look like you’re skipping town.” The mercenary crosses his arms and leans against a wall.

“If you ask me, it's not really the best time to take off. Not with the murderer on the loose and the rot this close to town.” The mercenary looks off to the side, seemingly noticing something. 

“Now if you were to explain why you are out here so late, maybe I’ll help you get out of here.” He gets off of the wall and turns to face you once more.

[b]What do you say to this stranger?[/b]
*fake_choice
    #What's it to you?
        You grab the bag off the ground and hold it to your chest. “What’s it to you?” 

        “Whoa now! Just asking a simple question. Not many are out this late, and honestly, carrying all that stuff makes it look like you’re skipping town.” The mercenary crosses his arms and leans against a wall.

        “If you ask me, it's not really the best time to take off. Not with the murderer on the loose and the rot this close to town.” The mercenary looks off to the side, seemingly noticing something. 

        “Now if you were to explain why you are out here so late, maybe I’ll help you get out of here.” He gets off of the wall and turns to face you once more.

    #Lie.
        “I’m just trying to get to my friend’s -” Before the lie can actually take form, the man cuts you off.

        “I’m going to stop you there. You have way too much stuff in that bag to visit somebody and even if you were moving in with your friend in the middle of the night, you are too jumpy for even sneaking off to a lover’s house. Now, why are you here? Before I call that guard back over,” he says.

You take a step back and sigh.

“I got caught up in something I had nothing to do with and I have to leave before it comes to get me.” You can feel your face flush as you explain.

“You are the one the guards are after,” a female voice says. You turn around to see a priestess at the entrance of the alley. Her brightly colored dress shines like a beacon in the moonlight.

*if (option = "run")
    “Yes, but I had nothing to do with that murder. All that happened was that I heard a scream and chased after him.” You look between the priestess and the mercenary, silently hoping they believed you.
    
*if (option = "leave")
    “Yes, but I had nothing to do with the murder, I didn’t even know something happened. I was walking home when I heard a scream and a random person bumped into me.”

*page_break

*image images/Cel_Storm.webp

“You would have packed your bags and left a long time ago if you were truly responsible for the murder.” The priestess walks to the other wall in the alley, pulling her dark colored cloak closer.

“The murder seemed to be too well planned for an amateur. If you knew what you were doing, you would have heard me sneak up on you.” The mercenary looks up as if he is thinking.

“I could get you out of this town and help you find the murderer, if you help me in return. Don’t worry, it’s nothing bad, just help us protect a few towns.” The mercenary looks over at you.

“Don’t join these ruffians. Come join me to the temple and we can protect you from the town. I won’t stand for this injustice,”  the priestess interrupts.

*choice
    #"I choose to go with the priestess and stay at the temple."
        "Welcome to the Flames of Sigel. I'm sure your friend would be proud of you. Come along. We'll head to the temple when we are done here." The priestess leads you to the local church.
        
        *set faction "divine"
        *finish

    #"I choose to go with the mercenary and find out who did this."
        "We'll find who did this, now we need to get you out of here. Thankfully I know some people nearby and you can stay there for the time being." The mercenary takes you into a building.
        
        *set faction "merc"
        *finish

    #Think about decision. (Reallocate Attribute Points)
        *set Stored_Attr_Total (Attr_PNT_Total + Brawn) + (Agility + Cunning)
        *page_break
        *goto CH4_Reallocate_Menu
*label C4_Caught
*set CH4_isCaught true
You remember seeing a path to the forest a few weeks ago. It’s risky, but not many people seem to take it. You gather all your belongings and start heading that way.

You are almost there when -

[b]“STOP!”[/b]

A loud shout freezes you in your place. You turn to see a guard, their bow drawn and aimed at you. You’ve been caught.

“Put your stuff on the ground and put your hands up!”

You comply, and when he gets to you, he puts your arm on your back and leads you to a carriage.

*page_break

Once you are at the prison, they throw you into a cell. There are only three cells in this prison. Crime isn't much of a problem here. The main injustices are just disputes between neighbors that don't like each other or an out of town person thinking they can get away with something. Murder is for the big cities, not roadside towns.

The room you are in is made of stone and you were lucky enough to be thrown into a cell with a window and a door, both with bars on them. The room you are in is also across from the guard's room.

You find the best looking corner and sit in it, questioning how this all happened. All you were trying to do is go home; you had nothing to do with this.

*page_break

*image images/mercenary_transparent_background.webp

“Hey, what are you in for?” A man you didn’t notice before walks up to the bars from a doorway. The room behind him sort of looks like an office. He doesn’t look like any guard you know. He has a dark green cloak on, but none of the guards wear a cloak like that. As he walks closer, you can see that he has a sword on his back and a dagger in his belt, along with some other shiny things across his clothing. None of the guards look like this, so he must be a mercenary passing though.

[b]What do you say?[/b]
*fake_choice
    #I'm innocent!
        “I got caught up in a misunderstanding. They think I murdered Lord Odel when all I was doing was trying to get home,” you explain, putting your head in your knees.

        “Sounds rough. I heard about Lord Odel, and checked out his body myself.” He gives you a once over before leaning against a nearby wall. “You don’t seem bloody enough to have done it. He had multiple stab wounds, and that would have made quite the mess.” 

        “The murder was also too well planned out for someone like you to be caught. Seems like they got a scapegoat.” He crosses his arms.

        “Guess so. All I wanted to do was go home and put away my ${bought}. Instead I get sent to prison for a crime I didn’t commit.” You sigh. The reality of what's happened begins to settle over you. All the plans you had for your life have suddenly been ripped away in one afternoon.

    #I've been through a lot.
        This last day has been a bumpy ride and a half for me. All I wanted to do was go to the market and buy something with the money I just got, but I’ve been framed for a murder instead.

        “Sounds rough. I heard about Lord Odel, and checked out his body myself.” He gives you a once over before leaning against a nearby wall. “You don’t seem bloody enough to have done it. He had multiple stab wounds, and that would have made quite the mess.” 

        “The murder was also too well planned out for someone like you to be caught. Seems like they got a scapegoat.” He crosses his arms.

        “Guess so.” You sigh. The reality of what's happened begins to settle over you. All the plans you had for your life have suddenly been ripped away in one afternoon.

*page_break

“Well, I could ask my buddies and see if we could get you out of here, maybe even find out who actually murdered the Lord. But you’ll have to help us in return. Nothing bad, just help us protect some towns and find some other bad guys. Nothing too bad, I think,” he explains before sharply looking at the door.

“Think about it. I’ll come back tonight for your answer.” And with that, he leaves through the door he just came in through. You stare at the space he was in moments ago, dumbfounded. That was... unexpected.

“You’d think he’d be quieter if he didn’t want to be caught.” A female’s voice calls out from the door this time. You blink, even more confused as a priestess walks in.

*page_break

*image images/Cel_Storm.webp

“I happened to overhear you two talking and I’ve heard that you managed to get yourself into a bit of trouble.” The priestess walks over to the bars. Her light colored dress and shining jewelry is completely at odds with the dingy prison walls.

“To counter his offer, I can take you to our temple. We can protect you from the town and anyone who comes after you, as long as you follow our rules and help out where you can,” she offers.

*fake_choice
    #Who is she?
        Who is this mysterious person? And who let her in to visit you? You give her a questioning look.

    #Why have you been getting so many visits?
        Just how common is it for strangers to visit prisons? You’d think the place would be more secure if it were holding someone accused for murder. You give her a questioning look.

“Oh, my apologies, I am a priestess of the Flames of Sigel. We protect towns from the upcoming darkness and house those in trouble,” she explains, “But I imagine you have a lot to think about. I’ll come back tonight and you can make your decision then.” With that, she leaves the room.

*page_break

Later that same night, the priestess walks in through the main door and the mercenary walks in through the office. The whole time you’ve been here, you’ve seen guards walk in and out of that room, but you never saw him leave it. He doesn’t seem particularly welcome here, either. Otherwise, why would he sneak around so much?

“I shouldn’t be surprised that you showed up. I heard you walking through the halls,” the mercenary says, looking at the priestess.

“Yes, well, I can’t just let ${pronoun_them} be hanged if there is a chance they are innocent,” the priestess responds. They almost seem to be familiar with each other, but you don’t have time to really think about that. Both of them are looking at you, expectantly.

“Now, have you made your choice?”

*label CH4_Final_Choice
*choice
    #"I choose to go with the priestess and stay at the temple."
        "I'll talk with the captain and get you released into my custody." The priestess walks out of the room. When you look over at the mercenary, he is gone.
        
        She comes back with the captain and he unlocks the gate.
        
        "Don't come back. I'm already going to be in a rot full of trouble because of this."

        *set faction "divine"
        *finish
        *comment *goto_scene Divine
        
    #"I choose to go with the mercenary and find out who did this."
        "I'll make sure we get them and clear your name. Until then you'll be under my custody." The mercenary grabs a key from his pocket.
        
        "You probably won't be allowed back into this town no matter what you choose. However, we'll make sure to keep you safe. Oh, and before I forget. While I couldn't get you your ${bought} back, I was able to snatch the dagger. We'll get it cleaned up for you."

        *set faction "merc"
        *finish
        *comment *goto_scene Mercenary
    
    #Think about decision. (Reallocate Attribute Points)
        *set Stored_Attr_Total (Attr_PNT_Total + Brawn) + (Agility + Cunning)
        *page_break
        *goto CH4_Reallocate_Menu

*comment Stat Designation Page
*label CH4_Reallocate_Menu

*comment Display the total of all the attribute points the player got
You currently have ${Attr_PNT_Total} unused attribute points.

*choice
    *selectable_if (((Brawn != 0) or (Agility != 0)) or (Cunning != 0)) #Reset All Attribute Points
        You have reset all your attribute points to 0.
        *set Attr_PNT_Total (Attr_PNT_Total + Brawn) + (Agility + Cunning)
        *set Stored_Attr_Total Attr_PNT_Total
        *set Brawn 0
        *set Agility 0
        *set Cunning 0
        *goto CH4_Reallocate_Menu
    *selectable_if ((Increase_Attr = false) and (Attr_PNT_Total > 0)) #Increase Attributes by +1
        Increase attribute mode enabled.
        *set Increase_Attr true
        *goto CH4_Reallocate_Menu
    *selectable_if ((Increase_Attr = true) and (Stored_Attr_Total > Attr_PNT_Total)) #Decrease Attributes by -1
        Decrease attribute mode enabled.
        *set Increase_Attr false
        *goto CH4_Reallocate_Menu

    *selectable_if (((Brawn > 0) and (Increase_Attr = false)) or (((Brawn < 10) and (Attr_PNT_Total > 0)) and (Increase_Attr = true))) #Modify [color=${brawn_color}]Brawn[/color]
        *if (Increase_Attr = false)
            You are currently decreasing attributes and have decreased your [color=${brawn_color}]Brawn[/color] by -1.
            *set Attr_PNT_Total Attr_PNT_Total + 1
            *set Brawn Brawn - 1
            *goto CH4_Reallocate_Menu
        *else
            You are currently increasing attributes and have increased [color=${brawn_color}]Brawn[/color] by +1.
            *set Attr_PNT_Total Attr_PNT_Total - 1
            *set Brawn Brawn + 1
            *goto CH4_Reallocate_Menu

    *selectable_if (((Agility > 0) and (Increase_Attr = false)) or (((Agility < 10) and (Attr_PNT_Total > 0)) and (Increase_Attr = true))) #Modify [color=${agility_color}]Agility[/color]
        *if (Increase_Attr = false)
            You are currently decreasing attributes and have decreased your [color=${agility_color}]Agility[/color] by -1.
            *set Attr_PNT_Total Attr_PNT_Total + 1
            *set Agility Agility - 1
            *goto CH4_Reallocate_Menu
        *else
            You are currently increasing attributes and have increased [color=${agility_color}]Agility[/color] by +1.
            *set Attr_PNT_Total Attr_PNT_Total - 1
            *set Agility Agility + 1
            *goto CH4_Reallocate_Menu

    *selectable_if (((Cunning > 0) and (Increase_Attr = false)) or (((Cunning < 10) and (Attr_PNT_Total > 0)) and (Increase_Attr = true))) #Modify [color=${cunning_color}]Cunning[/color]
        *if (Increase_Attr = false)
            You are currently decreasing attributes and have decreased your [color=${cunning_color}]Cunning[/color] by -1.
            *set Attr_PNT_Total Attr_PNT_Total + 1
            *set Cunning Cunning - 1
            *goto CH4_Reallocate_Menu
        *else
            You are currently increasing attributes and have increased [color=${cunning_color}]Cunning[/color] by +1.
            *set Attr_PNT_Total Attr_PNT_Total - 1
            *set Cunning Cunning + 1
            *goto CH4_Reallocate_Menu

    #TEMP: Gain 10 Attribute Points
        *set Attr_PNT_Total Attr_PNT_Total + 10
        *set Stored_Attr_Total Attr_PNT_Total
        *goto CH4_Reallocate_Menu

    #TEMP: Remove all Attribute Points
        *set Attr_PNT_Total 0
        *set Stored_Attr_Total Attr_PNT_Total
        *goto CH4_Reallocate_Menu

    #Finish Reallocation
        *if Attr_PNT_Total > 0
            You have unallocated attribute points. This is your last chance to use them.
            *choice
                #Use Remaining Points
                    *goto CH4_Reallocate_Menu
                #Continue
                    *if debug_reallocation = true
                        *set debug_reallocation false
                        *goto_scene startup
                    *goto CH4_Final_Choice
        *else
            *goto CH4_Final_Choice